WHITESPACE = _{ " " }
COMMENT    = _{ "//" ~ (!NEWLINE ~ ANY)* }

terminator = _{ NEWLINE+ | ";" ~ NEWLINE* }

bool         = @{ "true" | "false" }
number       = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
string       = @{ "\"" ~  ( "\\\"" | (!"\"" ~ ANY) )* ~ "\"" }
ident_char   = @{ ASCII_ALPHANUMERIC | "_" | "-" | "?" }
ident        = @{ ident_char+ }

top_level = _{
       SOI
     ~ NEWLINE*
     ~ profile
     ~ NEWLINE*
     ~ EOI
}


profile = {
      "profile"
    ~ string
    ~ "{"
    ~ NEWLINE*
    ~ (block ~ terminator)+
    ~ block?
    ~ "}"
}

assignment = {
      ident
    ~ "="
    ~ expr
}


block = _{
      define_block
    | named_block
    | when_block
}

define_block = {
      "define"
    ~ "{"
    ~ NEWLINE*
    ~ (assignment ~ terminator)+
    ~ assignment?
    ~ "}"
}

named_block = {
      ident
    ~ "{"
    ~ NEWLINE*
    ~ (expr ~ terminator)+
    ~ expr?
    ~ "}"
}

when_block = {
      "when"
    ~ "{"
    ~ NEWLINE*
    ~ (when_clause ~ terminator)+
    ~ when_clause?
    ~ "}"
}

when_clause = {
      (expr | "else")
    ~ "=>"
    ~ expr
}

expr = _{
      bool
    | number
    | string
    | tag_expr
    | block
    | ident
}

tag_expr = {
      "["
    ~ tag_expr_clause
    ~ (";" ~ tag_expr_clause)*
    ~ "]"
}

tag_expr_clause = {
      ident ~ "=" ~ tag_expr_pattern
    | ident ~ "!=" ~ tag_expr_pattern
    | "!" ~ ident
    | ident
}

tag_expr_pattern = {
      (ident | string) ~ ("|" ~ tag_expr_pattern)*
}
